set dotenv-load
set shell := ["bash", "-c"]
set windows-shell := ["powershell", "-NoLogo", "-Command"]

RUST_DIR := "src-tauri"

TAURI_CMD := "bun run tauri"
RM_CMD := if os_family() == "windows" { "Remove-Item -Force -Recurse -ErrorAction SilentlyContinue" } else { "rm -rf" }

alias s := setup
alias d := dev

alias b := build
alias bd := build-debug

alias cl := clean
alias clf := clean-front
alias clb := clean-back

alias l := lint
alias lf := lint-front
alias lb := lint-back

alias f := format
alias ff := format-front
alias fb := format-back

alias c := check
alias cf := check-front
alias cb := check-back

alias fi := fix
alias fif := fix-front
alias fib := fix-back

alias ta := test-all
alias tl := test-lib
alias tfa := test-front-all
alias tf := test-front
alias tba := test-back-all
alias tb := test-back

alias u := update
alias uf := update-front
alias ub := update-back

alias adf := add-dep-front
alias adb := add-dep-back

# -----------------------------------------------------------------------------
# Core Development & Build Commands
# -----------------------------------------------------------------------------

# List available commands
@_default:
    just --list --unsorted
    echo ""
    echo "ğŸ’¡ Use 'just setup' to prepare your environment."
    echo "ğŸ’¡ Use 'just dev' to start the development server."

# Setup the project environment
@setup:
    echo "ğŸš€ Setting up project dependencies..."
    -bun install
    cargo check --manifest-path {{ RUST_DIR }}/Cargo.toml
    echo "âœ… Setup complete! You can now run 'just dev'."

# Start the development server
@dev:
    echo "â–¶ï¸ Starting Tauri development server..."
    {{ TAURI_CMD }} dev

# Build the Tauri application
[group: "build"]
@build:
    echo "ğŸ“¦ Building Tauri application (release mode)..."
    {{ TAURI_CMD }} build

# Build the Tauri application in debug mode
[group: "build"]
@build-debug:
    echo "ğŸ“¦ Building Tauri application (debug mode)..."
    {{ TAURI_CMD }} build --debug

# Clean project artifacts
[group: "clean"]
[confirm: "Are you sure you want to clean the project artifacts? This will remove all build outputs."]
@clean:
    echo "ğŸ§¹ Cleaning project artifacts..."
    -{{ RM_CMD }} dist
    cd {{ RUST_DIR }}; cargo clean
    echo "âœ… Clean complete!"

# Clean front-end artifacts
[group: "clean"]
[confirm: "Are you sure you want to clean the front-end artifacts? This will remove all build outputs."]
@clean-front:
    echo "ğŸ§¹ Cleaning front-end artifacts..."
    -{{ RM_CMD }} dist
    echo "âœ… Front-end clean complete!"

# Clean back-end artifacts
[group: "clean"]
[confirm: "Are you sure you want to clean the back-end artifacts? This will remove all build outputs."]
[working-directory: 'src-tauri']
@clean-back:
    echo "ğŸ§¹ Cleaning back-end artifacts..."
    cargo clean
    echo "âœ… Back-end clean complete!"


# -----------------------------------------------------------------------------
# Formatting, Checking, Linting, and Fixing
# -----------------------------------------------------------------------------

# Formatting
[group: "format"]
@format:
    echo "ğŸ’… Formatting code..."
    -bunx biome format --write .
    cargo fmt --manifest-path {{ RUST_DIR }}/Cargo.toml --all
    echo "âœ… Formatting complete!"

# Front-end specific formatting
[group: "format"]
@format-front:
    echo "ğŸ’… Formatting front-end code..."
    bunx biome format --write .
    echo "âœ… Front-end formatting complete!"

# Back-end specific formatting
[group: "format"]
@format-back:
    echo "ğŸ’… Formatting back-end code..."
    cargo fmt --manifest-path {{ RUST_DIR }}/Cargo.toml --all
    echo "âœ… Back-end formatting complete!"

# Checking
[group: "check"]
@check:
    echo "ğŸ§ Running static analysis..."
    -bunx biome check .
    -bunx tsc --noEmit
    cargo check --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets
    echo "âœ… Checks complete!"

# Front-end specific checks
[group: "check"]
@check-front:
    echo "ğŸ§ Running front-end checks..."
    -bunx biome check .
    bunx tsc --noEmit
    echo "âœ… Front-end checks complete!"

# Back-end specific checks
[group: "check"]
@check-back:
    echo "ğŸ§ Running back-end checks..."
    cargo check --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets
    echo "âœ… Back-end checks complete!"

# Linting
[group: "lint"]
@lint:
    echo "ğŸ” Running linters..."
    -bunx biome lint .
    cargo clippy --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets
    echo "âœ… Linting complete!"

# Front-end specific linting
[group: "lint"]
@lint-front:
    echo "ğŸ” Running front-end linters..."
    bunx biome lint .
    echo "âœ… Front-end linting complete!"

# Back-end specific linting
[group: "lint"]
@lint-back:
    echo "ğŸ” Running back-end linters..."
    cargo clippy --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets
    echo "âœ… Back-end linting complete!"

# Fixing
[group: "fix"]
@fix:
    echo "ğŸ› ï¸ Fixing code issues..."
    bunx biome check --write .
    cargo clippy --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets --fix --allow-dirty
    echo "âœ… Fixing complete!"

# Front-end specific fixing
[group: "fix"]
@fix-front:
    echo "ğŸ› ï¸ Fixing front-end code issues..."
    bunx biome check --write .
    echo "âœ… Front-end fixing complete!"

# Back-end specific fixing
[group: "fix"]
@fix-back:
    echo "ğŸ› ï¸ Fixing back-end code issues..."
    cargo clippy --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets --fix --allow-dirty
    echo "âœ… Back-end fixing complete!"

# -----------------------------------------------------------------------------
# Testing
# -----------------------------------------------------------------------------

# Run all tests
# Note: Back-end tests use --test-threads=1 due to tokio::time::pause()
[group: "test"]
@test-all:
    echo "ğŸ§ª Running tests..."
    -bun run test:run
    cargo test --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace -- --test-threads=1
    echo "âœ… Tests complete!"

# Run library tests only
[group: "test"]
@test-lib *tests:
    echo "ğŸ§ª Running library tests..."
    cargo test --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --lib {{ tests }}
    echo "âœ… Library tests complete!"

# Run all front-end tests
[group: "test"]
@test-front-all:
    echo "ğŸ§ª Running front-end tests..."
    bun run test:run
    echo "âœ… Front-end tests complete!"
    
# Run front-end tests
[group: "test"]
@test-front +tests:
    echo "ğŸ§ª Running front-end tests..."
    bun run test:run {{ tests }}
    echo "âœ… Front-end tests complete!"

# Run all back-end tests
# Note: Uses --test-threads=1 because scheduler tests use tokio::time::pause()
# which affects global time and cannot run concurrently
[group: "test"]
@test-back-all:
    echo "ğŸ§ª Running back-end tests..."
    cargo test --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace -- --test-threads=1
    echo "âœ… Back-end tests complete!"

# Run back-end tests
[group: "test"]
@test-back +tests:
    echo "ğŸ§ª Running back-end tests..."
    cargo test --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace {{ tests }}
    echo "âœ… Back-end tests complete!"

# -----------------------------------------------------------------------------
# Helper Recipes
# -----------------------------------------------------------------------------

# Update dependencies
[group: "update-dependencies"]
@update:
    echo "â¬†ï¸ Updating dependencies..."
    -bun update
    cargo update --manifest-path {{ RUST_DIR }}/Cargo.toml
    echo "âœ… Dependencies updated!"

# Update front-end dependencies
[group: "update-dependencies"]
@update-front:
    echo "â¬†ï¸ Updating front-end dependencies..."
    bun update
    echo "âœ… Front-end dependencies updated!"

# Update back-end dependencies
[group: "update-dependencies"]
@update-back:
    echo "â¬†ï¸ Updating back-end dependencies..."
    cargo update --manifest-path {{ RUST_DIR }}/Cargo.toml
    echo "âœ… Back-end dependencies updated!"

# Add front-end dependency
[group: "add-dependency"]
@add-dep-front +deps:
    echo "â¬†ï¸ Adding front-end dependencies..."
    bun add {{ deps }}
    echo "âœ… Front-end dependencies added!"

# Add back-end dependency
[group: "add-dependency"]
@add-dep-back +deps:
    echo "â¬†ï¸ Adding back-end dependencies..."
    cargo add {{ deps }} --manifest-path {{ RUST_DIR }}/Cargo.toml
    echo "âœ… Back-end dependencies added!"

# -----------------------------------------------------------------------------
# Git
# -----------------------------------------------------------------------------

# Check before committing
[group: "git"]
@pre-commit-checks:
    echo "ğŸ”’ Running frontend checks..."
    bunx biome check .
    bunx tsc --noEmit
    echo "âœ… Frontend checks passed!"
    echo "ğŸ”’ Running backend checks..."
    cargo fmt --manifest-path src-tauri/Cargo.toml --all --check
    cargo check --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-target
    cargo clippy --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets -- -D warnings
    echo "âœ… Backend checks passed!"

# Check before committing
[group: "git"]
@pre-commit-checks-all:
    echo "ğŸ”’ Running front-end checks..."
    -bunx biome check .
    -bunx tsc --noEmit
    echo "âœ… Front-end checks passed!"
    echo "ğŸ”’ Running back-end checks..."
    -cargo fmt --manifest-path src-tauri/Cargo.toml --all --check
    -cargo check --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets
    -cargo clippy --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets -- -D warnings
    echo "âœ… Back-end checks passed!"

# Fix before committing
[group: "git"]
@pre-commit-fixes:
    echo "ğŸ’… Formatting front-end code..."
    -bunx biome format --write .
    echo "âœ… Front-end formatting applied!"
    echo "ğŸ’… Formatting back-end code..."
    -cargo fmt --manifest-path {{ RUST_DIR }}/Cargo.toml --all
    echo "âœ… Back-end formatting applied!"
    echo "ğŸ› ï¸ Fixing front-end code issues..."
    -bunx biome check --write .
    echo "âœ… Front-end fixing complete!"
    echo "ğŸ› ï¸ Fixing back-end code issues..."
    -cargo clippy --manifest-path {{ RUST_DIR }}/Cargo.toml --workspace --all-targets --fix --allow-dirty
    echo "âœ… Back-end fixing complete!"